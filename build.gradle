buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.townsfolk:gradle-release:1.2'
    }
}

ext {
    // note, publishing also requires signing configuration
    publish = project.properties.containsKey('sonatypeUsername')
}

apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'groovy'
apply plugin: 'release'

group 'com.github.scct'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from "$buildDir/docs/groovydoc"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

if (publish) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (publish) {
                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }
            }

            pom.project {
                name 'GradleSCCT'
                description 'gradle-scct is a Gradle plugin for calculating code coverage using SCCT'
                url 'https://github.com/sqality'

                scm {
                    url 'scm:git:https://github.com/sqality/gradle-scct.git'
                    developerConnection 'scm:git:https://github.com/sqality/gradle-scct.git'
                }

                licenses {
                    license {
                        name 'unlicense'
                        url 'http://unlicense.org'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'maiflai'
                    }
                    developer {
                        id 'ubourdon'
                    }
                }
            }
        }
    }
}
